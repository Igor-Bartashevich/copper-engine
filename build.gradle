apply plugin: 'idea'

allprojects {
    apply plugin: 'maven'
    apply plugin: 'project-report'

    /* for setting the group name determine the first hierarchy project */
    def groupProject = project;
    while (groupProject.parent != null && groupProject.parent.parent != null) {
        groupProject = groupProject.parent;
    }
    group = "de.scoopgmbh.copper." + groupProject.name.toLowerCase()

    repositories {
        mavenCentral()
    }
}

configure(subprojects.findAll { new File(it.projectDir, 'src/main/java').directory || new File(it.projectDir, 'src/main/schema').directory}) {
    println "configuring Leaf module " + project.path

    apply plugin: 'java'
    compileJava.options.encoding = 'utf-8'
    compileJava.sourceCompatibility = '1.6'
    compileJava.targetCompatibility = '1.6'
    compileTestJava.options.encoding = 'utf-8'
    compileTestJava.sourceCompatibility = '1.6'
    compileTestJava.targetCompatibility = '1.6'

    /*
    *  apply eclipse plugin only for 'deepest' subprojects
    *  since eclipse does not understand subprojects
    */
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    /*
    * be sure to always regenerate eclipse files, because
    * default behavior is merging into existing files
    */
    tasks.eclipse.dependsOn(cleanEclipse)
    eclipse.classpath.defaultOutputDir = new File("$buildDir/classes/main")
    
    dependencies {
        testCompile 'junit:junit:4.11'
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    task replace_headers << {
        def licenseFile = file("$rootDir/common/apache-license-file.txt")
        def licenseHeader
        	
        ant.loadfile(srcFile: licenseFile, property: 'licenseHeader')
        	
        licenseHeader = "${ant.properties['licenseHeader']}"
        	
        println "Replacing java source file headers in "+project.path
        // println licenseHeader
           	
        licenseHeader = licenseHeader + "package "
          	
        ant.replaceregexp(match: '(/\\*.*\\*/.*)??^package ', flags: 'sm', replace: licenseHeader) {
            fileset(dir: "$projectDir/src") {
                include(name: '**/*.java')
            }
        }
    }
    compileJava.dependsOn replace_headers    
    
    
    /// ********
        // build additional artifacts
 
        task sourcesJar(type: Jar, dependsOn:classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn:javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        testJava = file("src/test/java").isDirectory() 

        if (testJava) {
            task testJar(type: Jar, dependsOn:testClasses) {
                classifier = 'test'
                from sourceSets.test.output
            }
	
            task testSourcesJar(type: Jar, dependsOn:testJar) {
                classifier = 'testSources'
                from sourceSets.test.allSource
            }

            assemble.dependsOn testJar
            assemble.dependsOn testSourcesJar
        }

        databaseScripts = file("src/main/database").isDirectory() 

        if (databaseScripts) {
            task scriptsZip(type: Zip) {
                classifier = 'scripts'
                from file("src/main/database")
                into 'scripts/sql'
            }

            assemble.dependsOn scriptsZip
        }

        artifacts {      
	        archives sourcesJar      
        	archives javadocJar
            if (testJava) {
	           archives testJar
	           archives testSourcesJar
            }
            if (databaseScripts) {
               archives scriptsZip
            }
        }   
}

project(':projects:copper-coreengine') {

    sourceSets {
        workflow {
            ext.srcDir = "$projectDir/src/workflow/java"
        }
    }
    sourceSets.test.resources.srcDirs += sourceSets.workflow.srcDir
        
        
    dependencies {
		compile 'aopalliance:aopalliance:1.0'
		
		// asm
		compile 'asm:asm:3.3.1'
		compile 'asm:asm-commons:3.3.1'
		compile 'asm:asm-tree:3.3.1'
		compile 'asm:asm-util:3.3.1'
		compile 'asm:asm-analysis:3.3.1'

		// commons
		compile 'commons-codec:commons-codec:1.4'
		compile 'commons-logging:commons-logging:1.1.1'
		
		// logging
		compile 'org.slf4j:slf4j-api:1.6.6'
		compile 'org.slf4j:slf4j-log4j12:1.6.6'
		compile 'log4j:log4j:1.2.17'
		 
		// Spring
		compile 'org.springframework:spring-aop:3.1.2.RELEASE'
		compile 'org.springframework:spring-asm:3.1.2.RELEASE'
		compile 'org.springframework:spring-beans:3.1.2.RELEASE'
		compile 'org.springframework:spring-context:3.1.2.RELEASE'
		compile 'org.springframework:spring-core:3.1.2.RELEASE'
		compile 'org.springframework:spring-expression:3.1.2.RELEASE'
		compile 'org.springframework:spring-jdbc:3.1.2.RELEASE'
		compile 'org.springframework:spring-tx:3.1.2.RELEASE'
		
		compile 'org.springframework.batch:spring-batch-infrastructure:2.1.8.RELEASE'
		
		// Database
		compile 'c3p0:c3p0:0.9.1.2'
		compile 'c3p0:c3p0-oracle-thin-extras:0.9.0.2'
		compile 'mysql:mysql-connector-java:5.1.21'
		compile 'org.apache.derby:derby:10.9.1.0'
		compile 'postgresql:postgresql:9.1-901.jdbc4'    
		
		compile fileTree(dir: project(':').projectDir.toString()+'/3rdPartyLibs', include: '*.jar')		
    }

    test {
        //configuring a system property for tests
        systemProperty 'copper.unittest.skip.external.db.tests', 'true'
    }
}

project(':projects:copper-examples:orchestration:orch-engine') {
    sourceSets {
        workflow {
            ext.srcDir = "$projectDir/src/workflow/java"
        }
    }
    sourceSets.main.resources.srcDirs += sourceSets.workflow.srcDir
    
    dependencies {
	compile project(':projects:copper-coreengine')
	compile project(':projects:copper-examples:orchestration:orch-interfaces')
	compile 'log4j:log4j:1.2.17'
    }
}

project(':projects:copper-examples:orchestration:orch-interfaces') {

    configurations {
        wsgen
    }

    sourceSets {
        schema {
            ext.srcDir = "$projectDir/src/main/schema"
        }
    }
    sourceSets.main.resources.srcDirs += sourceSets.schema.srcDir
    
    dependencies {
		
	compile project(':projects:copper-coreengine')
	compile 'log4j:log4j:1.2.17'

    	compile ('org.apache.cxf:cxf-rt-transports-http-jetty:2.5.3') {
    		exclude module: 'spring-web'
    	}
    	compile ('org.apache.cxf:cxf-rt-frontend-jaxws:2.5.3') {
    		exclude module: 'spring-web'
    	}
    	compile 'org.eclipse.jetty:jetty-server:7.5.4.v20111024'
        compile 'commons-lang:commons-lang:2.6'

        wsgen 'org.apache.cxf:cxf-tools-wsdlto-core:2.6.0'
        wsgen 'org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:2.6.0'
        wsgen 'org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:2.6.0'
        wsgen 'org.apache.cxf:cxf-xjc-ts:2.2.12'
        wsgen 'com.sun.xml.bind:jaxb-xjc:2.2.4-1'		
    }

	tasks.add(name: "gen_wsbindings") {
		ext.genDirName = "$buildDir/gen.wsdls.src"
			
		inputs.dir new File(sourceSets.schema.srcDir)
    	outputs.dir new File(ext.genDirName)

        doFirst {
        	new File(ext.genDirName).mkdirs()
        }    	
		doLast {
	        fileTree(dir: sourceSets.schema.srcDir + "/wsdl", include: "**/*.wsdl", exclude: "common.wsdl").each { def wsdlFile -> 
				javaexec {					
			        main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
			        classpath = configurations.wsgen
			        args '-fe', 'jaxws',	        		
			                '-db', 'jaxb',
			                '-xjc-extension',
			                '-impl', '-server', '-client', '-validate',
			                '-d', ext.genDirName,
                            '-wsdlLocation', 'classpath:wsdl/'+wsdlFile.name,
			                wsdlFile
			    }	            
		    } 
        }		
	}
	sourceSets.main.java.srcDirs += gen_wsbindings.genDirName	
	
	compileJava.dependsOn gen_wsbindings
	eclipseClasspath.dependsOn gen_wsbindings

    idea {
        module {
            excludeDirs -= file('build')
        }
    }

}

project(':projects:copper-examples:orchestration:orch-simulators') {
    dependencies {
	compile project(':projects:copper-coreengine')
	compile project(':projects:copper-examples:orchestration:orch-interfaces')
	compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-api:1.6.6'
    }
}

